plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

/*
Create a file "constants.properties" in root folder for the api key and base url
to avoid upload personal API KEY to the public repository.

EX:
API_KEY="xxxxxxxxxxxxx9399539593"
BASE_URL="https://newsapi.org/v2/"
 */
def constantsPropertiesFile = rootProject.file("constants.properties")
def properties = new Properties()
properties.load(new FileInputStream(constantsPropertiesFile))

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.leandro.news"
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "API_KEY", properties['API_KEY'])
        buildConfigField("String", "BASE_URL", properties['BASE_URL'])

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    flavorDimensions "source"
    productFlavors{
        bbc {
            dimension "source"
            buildConfigField("String", "SOURCE", "\"bbc-news\"")
        }

        axios {
            dimension "source"
            buildConfigField("String", "SOURCE", "\"axios\"")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    // Kotlin
    implementation 'androidx.core:core-ktx:1.7.0'

    // Compose UI
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation "com.google.accompanist:accompanist-swiperefresh:0.24.2-alpha"
    
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1"
    implementation "androidx.activity:activity-compose:1.6.0-alpha03"

    def nav_version = "2.4.2"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:$dagger_hilt"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_hilt"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    implementation 'androidx.test:runner:1.4.0'
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Okhttp3
    def okhttp_version = '4.9.1'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Moshi Adapters
    implementation "com.squareup.moshi:moshi-adapters:1.5.0"

    // Image Library
    implementation "io.coil-kt:coil-compose:2.0.0-rc01"

    //Tests
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.9.1"
    testImplementation "io.mockk:mockk:1.11.0"

    debugImplementation "androidx.compose.ui:ui-test-manifest:1.2.0-beta01"
    debugImplementation "androidx.test:core:1.4.0"
}